/**
 * Configure Maven publishing for all projecs that have the "maven" plugin applied...
 */
plugins.withType(MavenPlugin) {

    /**
     * Configures the remote publishing repo.  Uses the "old" Gradle Maven plugin
     * (http://www.gradle.org/docs/current/userguide/maven_plugin.html).
     *
     * Publish to your local repo: gradle install
     * Publish to remote repo: gradle uploadArchives
     */
    uploadArchives {
        repositories {
            mavenDeployer {

                //
                // The cedexisMaven* variables come from your ~/.gradle/gradle.properties
                //

                repository(url: cedexisMavenPublicReleases) {
                    authentication(userName: cedexisMavenUser, password: cedexisMavenPassword)
                }
                snapshotRepository(url: cedexisMavenPublicSnapshots) {
                    authentication(userName: cedexisMavenUser, password: cedexisMavenPassword)
                }
            }
        }
    }

    // PUBLISHES SRC AND JAVADOC TO MAVEN
    task sourceJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: [javadoc]) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }
    artifacts {
        archives sourceJar
        archives javadocJar
    }
}